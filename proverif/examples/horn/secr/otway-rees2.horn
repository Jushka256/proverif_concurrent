pred c/1 decompData, elimVar.

fun host/1.

fun encrypt/2.

query c:secretA[].
query c:secretB[].

reduc

(* attacker *)
c:encrypt(m,k) & c:k -> c:m;
c:x & c:y -> c:encrypt(x,y);
c:x -> c:host(x);


c:host(shrA[]);
c:host(shrB[]);

(* A *)

c:X -> c:N[X];
c:X -> c:encrypt((Na[X], N[X], host(shrA[]), X), shrA[]);

c:encrypt((Na[host(shrB[])],key), shrA[]) -> c:encrypt(secretA[], key);

(* B *)

c:x & c:y -> c:encrypt((Nb[y],x,y,host(shrB[])), shrB[]);

c:encrypt((Nb[host(shrA[])],key), shrB[]) -> c:encrypt(secretB[], key);

(* Server *)

c:x & c:host(shra) & c:host(shrb) 
& c:encrypt((na, x,host(shra), host(shrb)), shra) 
& c:encrypt((nb, x,host(shra), host(shrb)), shrb)
-> c:encrypt((na, k[x,shra,shrb]), shra);

c:x & c:host(shra) & c:host(shrb) 
& c:encrypt((na, x,host(shra), host(shrb)), shra) 
& c:encrypt((nb, x,host(shra), host(shrb)), shrb)
-> c:encrypt((nb,k[x,shra,shrb]), shrb).


(* EXPECTPV
RESULT goal unreachable: c:secretB[]
RESULT goal unreachable: c:secretA[]
0.008s (user 0.008s + system 0.000s), max rss 8608K
END *)
