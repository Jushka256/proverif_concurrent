(* Needham Shroeder public key protocol.
   Original version, with a flaw discovered by Lowe *)

pred c/1 elimVar,decompData.
nounif c:x.

fun host/1.
fun sign/2.
fun pk/1.
fun encrypt/2.

query c:secret[].

not c:sS[].
not c:sA[].
not c:sB[].

reduc

(* The attacker *)

c:x & c:y -> c:sign(x,y);
c:sign(x,y) -> c:x;
c:x & c:encrypt(m,pk(x)) -> c:m;
c:x -> c:pk(x);
c:x & c:y -> c:encrypt(x,y);
c:host(x);

(* The protocol *)
(* Initialisation *)

c:pk(sA[]);
c:pk(sB[]);
c:pk(sS[]);

(* A *)

c:sign((pkx, host(x)), sS[]) -> c:encrypt((Na[pkx], host(sA[])), pkx);
c:sign((pkx, host(x)), sS[]) & c:encrypt((Na[pkx], y, host(x)), pk(sA[]))
   -> c:encrypt((y,k[x]), pkx);

(* B *)

c:encrypt((x,host(a)), pk(sB[])) & c:sign((pka, host(a)), sS[]) -> c:encrypt((x, Nb[x,a], host(sB[])), pka);
c:encrypt((x,host(sA[])), pk(sB[])) & c:sign((pka, host(sA[])), sS[]) & c:encrypt((Nb[x, sA[]], z), pk(sB[]))
   -> c:encrypt(secret[], pk(z));

(* server *)

c:(host(s1),host(s2)) -> c:sign((pk(s2), host(s2)), sS[]).

(* EXPECTPV
RESULT goal unreachable: c:secret[]
0.004s (user 0.004s + system 0.000s), max rss 8064K
END *)
