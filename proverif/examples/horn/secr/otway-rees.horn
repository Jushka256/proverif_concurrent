(* Otway Rees protocol.
   Correct version, from Millen, Ruess, 
   2000 IEEE Symposium on Security and Privacy *)

pred c/1 decompData, elimVar.

fun host/1.

fun encrypt/2.

query c:secretA[].
query c:secretB[].

reduc

(* attacker *)
c:encrypt(m,k) & c:k -> c:m;
c:x & c:y -> c:encrypt(x,y);
c:x -> c:host(x);


c:host(shrA[]);
c:host(shrB[]);

(* A *)

c:X -> c:(N[X],host(shrA[]),X, encrypt((Na[X], N[X], host(shrA[]), X), shrA[]));

c:(N[host(shrB[])], encrypt((Na[host(shrB[])],key), shrA[])) -> c:encrypt(secretA[], key);

(* B *)

c:(x,y,host(shrB[]),z) -> c:(x,y,host(shrB[]),z,encrypt((Nb[y],x,y,host(shrB[])), shrB[]));

c:(x,y,host(shrB[]),z) & c:(x,y2,encrypt((Nb[y], key), shrB[])) -> c:(x,y2);

c:(x,host(shrA[]),host(shrB[]),z) & c:(x,y2,encrypt((Nb[host(shrA[])],key), shrB[])) -> c:encrypt(secretB[], key);

(* Server *)

c:(x,host(shra), host(shrb), encrypt((na, x,host(shra), host(shrb)), shra),
   encrypt((nb,x,host(shra),host(shrb)), shrb)) ->
c:(x,encrypt((na, k[x,shra,shrb]), shra),encrypt((nb,k[x,shra,shrb]), shrb)).


(* EXPECTPV
RESULT goal unreachable: c:secretB[]
RESULT goal unreachable: c:secretA[]
0.012s (user 0.012s + system 0.000s), max rss 8680K
END *)
