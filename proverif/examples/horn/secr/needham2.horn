(* Needham Shroeder public-key protocol
   Corrected version of Lowe *)

pred c/1 decompData, elimVar.

fun pk/1.
fun encrypt/2.

query c:secret[].

reduc
(* Initialization *)

c:c[];
c:pk(sA[]);
c:pk(sB[]);

(* The attacker *)

c:x & c:encrypt(m,pk(x)) -> c:m;
c:x -> c:pk(x);
c:x & c:y -> c:encrypt(x,y);

(* The protocol *)
(* A *)

c:pkX -> c:encrypt((Na[pkX], pk(sA[])), pkX);
c:pkX & c:encrypt((Na[pkX], y, pkX), pk(sA[]))
   -> c:encrypt((y,k[pkX]), pkX);

(* B *)

c:encrypt((x,y), pk(sB[])) -> c:encrypt((x, Nb[x,y], pk(sB[])), y);
c:encrypt((x,pk(sA[])), pk(sB[])) & c:encrypt((Nb[x, pk(sA[])], z), pk(sB[]))
   -> c:encrypt(secret[], pk(z)).

(* EXPECTPV
RESULT goal unreachable: c:secret[]
0.004s (user 0.004s + system 0.000s), max rss 8156K
END *)
