(* Otway Rees protocol.
   Flawed version, quoted by Paulson TR443 Inductive... *)

free c.

(* Shared-key cryptography *)

fun encrypt/2.
reduc decrypt(encrypt(m,k),k) = m.

(* Host name / key *)

fun host/1.
private reduc getkey(host(x)) = x.

(* Secrecy assumptions *)

not kA.
not kB.

private free secretA, secretB.
query attacker:secretA;
      attacker:secretB.
query evinj:endAparam(x) ==> evinj:beginAparam(x).
query evinj:endAkey(x,y,z) ==> evinj:beginAkey(x,y,z).
query evinj:endBparam(x) ==> evinj:beginBparam(x).
query evinj:endBkey(x,y,z) ==> evinj:beginBkey(x,y,z).

let processA = 
	in(c, hostB0);
	event beginBparam(hostB0);
	new Na;
        out(c, (Na, hostA, hostB0, encrypt((Na, hostA, hostB0), kA)));
	in(c, (=Na, m2));
	let (=Na, k) = decrypt(m2, kA) in
	event beginBkey(hostA, hostB0, k);
	(* OK *)
	if hostB0 = hostB then 
        event endAparam(hostA);
	event endAkey(hostA, hostB0, k);
	out(c, encrypt(secretA, k)).

let processB = 
	in(c, (na, hostA1, =hostB, z));
	event beginAparam(hostA1);
	new Nb;
	out(c, (na, hostA1, hostB, z, Nb, encrypt((na, hostA1, hostB), kB)));
	in(c, (=na, m3, m4));
	let (=Nb, k) = decrypt(m4, kB) in
	event beginAkey(hostA1, hostB, k);
	out(c, (na, m3));
	(* OK *)
	if hostA1 = hostA then
	event endBparam(hostB);
	event endBkey(hostA1, hostB, k);
	out(c, encrypt(secretB, k)).

let processS = in(c, (Na1, hostA1, hostB1, m2, Nb1, m3));
	       let (=Na1, =hostA1, =hostB1) = decrypt(m2, getkey(hostA1)) in
	       let (=Na1, =hostA1, =hostB1) = decrypt(m3, getkey(hostB1)) in
	       new k;
               out(c, (Na1, encrypt((Na1, k), getkey(hostA1)), 
                            encrypt((Nb1, k), getkey(hostB1)))).


process new kA; new kB;
	let hostA = host(kA) in 
	let hostB = host(kB) in
	out(c, hostA); out(c, hostB);
	((!processA) | (!processB) | (!processS))

(* EXPECTPV
RESULT not attacker:secretA[] is true.
RESULT not attacker:secretB[] is false.
RESULT evinj:endAparam(x) ==> evinj:beginAparam(x) is false.
RESULT (even ev:endAparam(x) ==> ev:beginAparam(x) is false.)
RESULT evinj:endAkey(x,y,z_1) ==> evinj:beginAkey(x,y,z_1) is false.
RESULT (even ev:endAkey(x,y,z_1) ==> ev:beginAkey(x,y,z_1) is false.)
RESULT evinj:endBparam(x) ==> evinj:beginBparam(x) is false.
RESULT (even ev:endBparam(x) ==> ev:beginBparam(x) is false.)
RESULT evinj:endBkey(x,y,z_1) ==> evinj:beginBkey(x,y,z_1) is false.
RESULT (even ev:endBkey(x,y,z_1) ==> ev:beginBkey(x,y,z_1) is false.)
0.052s (user 0.052s + system 0.000s), max rss 11232K
END *)
