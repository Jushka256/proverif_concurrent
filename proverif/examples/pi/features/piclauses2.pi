(* Otway Rees protocol.
   Flawed version, quoted by Paulson TR443 Inductive... *)

free c.

(* Shared-key cryptography *)

fun encrypt/2.
reduc decrypt(encrypt(m,k),k) = m.

(* Host name / key *)

fun host/1.
private reduc getkey(host(x)) = x.

(* Secrecy assumptions *)

not kA.
not kB.

query ev:endAparam(x) ==> evinj:beginAparam(x).
query ev:endAkey(x,y,z) ==> evinj:beginAkey(x,y,z).
query ev:endBparam(x) ==> evinj:beginBparam(x).
query ev:endBkey(x,y,z) ==> evinj:beginBkey(x,y,z).

data cons/2.
data signatureElement/1.

(* Clauses *)

pred member/2 memberOptim.
pred mypred/2.

clauses

member:x,cons(x,l);
member:x,l -> member:x,cons(y,l);

x = signatureElement(y) -> mypred:x,y.



private free secretA, secretB.
query attacker:secretA.
query attacker:secretB.

let processA = 
	in(c, hostB0);
	event beginBparam(hostB0);
	new Na;
        out(c, (Na, hostA, hostB0, encrypt((Na, hostA, hostB0), kA)));
	in(c, (=Na, m2));
	let (=Na, k) = decrypt(m2, kA) in
	let x suchthat member:x,k in
	event beginBkey(hostA, hostB0, k);
	(* OK *)
	if hostB0 = hostB then 
        event endAparam(hostA);
	event endAkey(hostA, hostB0, k);
	out(c, encrypt(secretA, k)).

let processB = 
	in(c, (na, hostA1, =hostB, z));
	event beginAparam(hostA1);
	new Nb;
	out(c, (na, hostA1, hostB, z, Nb, encrypt((na, hostA1, hostB), kB)));
	in(c, (=na, m3, m4));
	let (=Nb, k) = decrypt(m4, kB) in
	event beginAkey(hostA1, hostB, k);
	out(c, (na, m3));
	(* OK *)
	if hostA1 = hostA then
	event endBparam(hostB);
	event endBkey(hostA1, hostB, k);
	out(c, encrypt(secretB, k)).

let processS = in(c, (Na1, hostA1, hostB1, m2, Nb1, m3));
	       let (=Na1, =hostA1, =hostB1) = decrypt(m2, getkey(hostA1)) in
	       let (=Na1, =hostA1, =hostB1) = decrypt(m3, getkey(hostB1)) in
	       new k;
               out(c, (Na1, encrypt((Na1, k), getkey(hostA1)), 
                            encrypt((Nb1, k), getkey(hostB1)))).


process new kA; new kB;
	let hostA = host(kA) in 
	let hostB = host(kB) in
	out(c, hostA); out(c, hostB);
	((!processA) | (!processB) | (!processS))

(* EXPECTPV
RESULT evinj:endAparam(x_1) ==> evinj:beginAparam(x_1) is true.
RESULT evinj:endAkey(x_1,y,z_1) ==> evinj:beginAkey(x_1,y,z_1) is true.
RESULT evinj:endBparam(x_1) ==> evinj:beginBparam(x_1) is false.
RESULT (even ev:endBparam(x_1) ==> ev:beginBparam(x_1) is false.)
RESULT evinj:endBkey(x_1,y,z_1) ==> evinj:beginBkey(x_1,y,z_1) is false.
RESULT (even ev:endBkey(x_1,y,z_1) ==> ev:beginBkey(x_1,y,z_1) is false.)
RESULT not attacker:secretA[] is true.
RESULT not attacker:secretB[] is false.
0.039s (user 0.035s + system 0.004s), max rss 11280K
END *)
