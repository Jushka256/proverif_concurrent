(* Denning-Sacco
   In this model, I separate the keys for encryption and the
   keys for signature, which complicates the model. *)

free c: channel.

type host.
type key.
type pkey.
type skey.
type spkey.
type sskey.

(* Probabilistic public key encryption *)

type seed.
fun pk(skey): pkey.
fun internal_encrypt(bitstring, pkey, seed): bitstring.
reduc forall x: bitstring, y: skey, r: seed; 
        decrypt(internal_encrypt(x,pk(y),r),y) = x.
letfun encrypt(x: bitstring, y:pkey) = new r: seed; internal_encrypt(x,y,r).

(* Signatures *)

fun spk(sskey): spkey.
fun sign(bitstring, sskey): bitstring.
reduc forall m: bitstring, k: sskey; getmess(sign(m,k)) = m.
reduc forall m: bitstring, k: sskey; checksign(sign(m,k), spk(k)) = m.

(* Probabilistic shared key encryption *)

type sseed.
fun internal_sencrypt(bitstring,key,sseed): bitstring.
reduc forall x: bitstring, y: key, r: sseed; sdecrypt(internal_sencrypt(x,y,r),y) = x.
letfun sencrypt(x: bitstring, y: key) = new r: sseed; internal_sencrypt(x,y,r).

(* Secrecy assumptions *)

not attacker(new skA).
not attacker(new skB).

(* Queries *)

free secretA, secretB: bitstring [private].
noninterf secretA, secretB.

(* Initiator *)

let processInitiator(skA: sskey, pkA: spkey, pkB: pkey) =
        in(c, pk2: pkey);
	new k: key;
	out(c, encrypt(sign((pkA, pk2, k), skA), pk2));
	if pk2 = pkB then
	(
	in(c, m: bitstring);
	let s = sdecrypt(m,k)
	)
	|
	out(c, sencrypt(secretA, k)).

(* Responder *)

let processResponder(skB: skey, pkB: pkey, pkA: spkey) =
	in(c, km: bitstring);
	let ks = decrypt(km,skB) in
	let (=pkA, =pkB, k:key) = checksign(ks, pkA) in
	(
	in(c, m: bitstring);
	let s = sdecrypt(m,k)
	)
	|
	out(c, sencrypt(secretB, k)).

(* Main process *)

process new skA: sskey; 
	let pkA = spk(skA) in
        out(c, pkA);
        new skB: skey; 
	let pkB = pk(skB) in
        out(c, pkB);
	((!processInitiator(skA, pkA, pkB)) | 
	 (!processResponder(skB, pkB, pkA)))


(* EXPECTPV
RESULT Non-interference secretA, secretB is true.
0.020s (user 0.016s + system 0.004s), max rss 10320K
END *)
